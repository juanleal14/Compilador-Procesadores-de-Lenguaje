/*LUCAS MONZÓN CASTAÑO JUAN LEAL ALIAGA
  100473232 100473223*/
%{                          // SECTION 1 Declarations for C-Bison
#include <stdio.h>
#include <ctype.h>            // tolower()
#include <string.h>           // strcmp() 
#include <stdlib.h>           // exit()

#define FF fflush(stdout);    // to force immediate printing 

int yylex () ;
void yyerror () ;
char *my_malloc (int) ;
char *gen_code (char *) ;
char *int_to_string (int) ;
char *char_to_string (char) ;

char temp [2048] ;

// Definitions for explicit attributes

typedef struct s_attr {
    int value ;    // - Numeric value of a NUMBER 
    char *code ;   // - to pass IDENTIFIER names, and other translations 
} t_attr ;

#define YYSTYPE t_attr     // stack of PDA has type t_attr

%}

// Definitions for explicit attributes

%token NUMBER        
%token IDENTIF       // Identifier=variable
%token INTEGER       // token for integer type
%token STRING        // token for string type
%token MAIN          // token for keyword main 
%token WHILE         // token for keyword while
%token PRINT
%token SETQ
%token SETF
%token DEFUN
%token PRIN1
%token LOOP
%token DO
%token PROGN
%token IF
%token MAKE_ARRAY
%token AREF
%token RETURN_FROM

%token OR
%token AND
%token MOD
%token DIFF
%token NOT
%token GTE
%token LTE
                    // last operation to do 
%left OR
%left AND
%left '=' DIFF
%left '>' '<' GTE LTE
%left '+' '-'                 // lowest precedence
%left '*' '/' MOD             // 
%right NOT
%left UNARY_SIGN              // highest precedence

%%                            // Section 3 Grammar - Semantic Actions

axiom:  '(' declaration_section   function_section  { ; }
;
declaration_section:  SETQ IDENTIF REST_SETQ ')'   { printf ("variable %s\n%s", $2.code, $3.code);} '('        declaration_section { ; }
                    |    /* lambda */       { ; }
                    ;
function_section:  DEFUN IDENTIF '('  ')' axiom_func ')' { printf (": %s\n%s; ", $2.code, $5.code) ;} '('        function_section { ; }
            |   DEFUN MAIN '('  ')' axiom_func ')'   { printf ("\n: main\n%s ;", $5.code) ;}
            ;

sentence:   '(' SETQ IDENTIF REST_SETQ ')'         { sprintf (temp, "variable %s\n%s", $3.code, $4.code) ; 
                                               $$.code = gen_code (temp) ; }
            | '(' SETF  REST_SETF ')'         { sprintf (temp, "%s",$3.code) ; 
                                               $$.code = gen_code (temp) ; }
            | '(' PRINT REST_PRINT ')'          {sprintf(temp, "%s", $3.code);
                                               $$.code = gen_code (temp) ; }
            | '(' PRIN1 REST_PRINT ')'          {sprintf(temp, "%s", $3.code);
                                                    $$.code = gen_code (temp) ; }
            | '(' LOOP WHILE expression DO axiom_func ')' {sprintf(temp,"begin %s while %s repeat", $4.code, $6.code);
                                                            $$.code = gen_code(temp);}
            | '(' IF expression '(' PROGN axiom_func ')'  REST_IF ')' {sprintf(temp,"%s if %s %s then",$3.code,$6.code,$8.code);
                                                                        $$.code = gen_code(temp);}
            | expression                             {sprintf(temp,"%s",$1.code);
                                                    $$.code = gen_code(temp);} 
            | '('RETURN_FROM IDENTIF expression ')' {sprintf(temp,"%s exit",$4.code);
                                                    $$.code = gen_code(temp);}
            ;
REST_SETQ: '(' MAKE_ARRAY  NUMBER ')'        {sprintf(temp,"%s %d cells allot ", $0.code, $3.value);
                                            $$.code = gen_code(temp);}
            | NUMBER                        {sprintf(temp,"%d %s ! ", $1.value, $0.code);
                                            $$.code = gen_code(temp);}
            ;
REST_SETF:   '(' AREF  IDENTIF expression ')' expression  {sprintf(temp," %s %s %s cells + ! ", $6.code, $3.code, $4.code);
                                                            $$.code = gen_code(temp);}
            | IDENTIF expression                            {sprintf(temp,"%s %s ! ", $2.code, $1.code);
                                                            $$.code = gen_code(temp);}
            ;
REST_IF:   '(' PROGN axiom_func ')'             {sprintf(temp, "else %s", $3.code);
                                                $$.code = gen_code(temp);}
            | /* lambda */                      { strcpy(temp,"");
                                    $$.code = gen_code(temp);}
            ;
axiom_func: sentence axiom_func { sprintf (temp,"%s\n%s", $1.code, $2.code) ; 
                                    $$.code = gen_code(temp);}
            | /*lambda*/            { strcpy(temp,"");
                                    $$.code = gen_code(temp);}
            ;
REST_PRINT: expression                 { sprintf (temp, "%s .", $1.code) ;
                                               $$.code = gen_code (temp) ; }
            | STRING                    { sprintf (temp, ".\" %s \"", $1.code);
                                               $$.code = gen_code (temp) ; }
            ;
expression:   term                           { $$ = $1 ; }
            | '(' '+' expression  expression  ')'    { sprintf (temp, "%s %s +", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' '-' expression  expression  ')'    { sprintf (temp, "%s %s -", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' '*' expression  expression ')'     { sprintf (temp, "%s %s *", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' '/' expression  expression ')'     { sprintf (temp, "%s %s /", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' AND expression  expression  ')'    { sprintf (temp, "%s %s and", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' OR expression  expression  ')'    { sprintf (temp, "%s %s or", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' NOT expression  ')'     { sprintf (temp, "%s  0=", $3.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' DIFF expression  expression ')'     { sprintf (temp, "%s %s = 0=", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' '=' expression  expression  ')'    { sprintf (temp, "%s %s =", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' '<' expression  expression  ')'    { sprintf (temp, "%s %s <", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' LTE expression  expression ')'     { sprintf (temp, "%s %s <=", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' '>' expression  expression ')'     { sprintf (temp, "%s %s >", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }                                   
            | '(' GTE expression  expression  ')'    { sprintf (temp, "%s %s >=", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            | '(' MOD expression  expression  ')'    { sprintf (temp, "%s %s mod", $3.code, $4.code) ;
                                               $$.code = gen_code (temp) ; }
            ;

term:         operand                        { $$ = $1 ; }                          
            | '+' operand %prec UNARY_SIGN   { sprintf (temp, "%s", $2.code) ;
                                               $$.code = gen_code (temp) ; }
            | '-' operand %prec UNARY_SIGN   { sprintf (temp, "%s negate", $2.code) ;
                                               $$.code = gen_code (temp) ; }    
            ;

operand:      IDENTIF                        { sprintf (temp, "%s @", $1.code) ;
                                               $$.code = gen_code (temp) ; }
            | NUMBER                         { sprintf (temp, "%d", $1.value) ;
                                               $$.code = gen_code (temp) ; }
            | '(' AREF IDENTIF expression ')' {sprintf(temp, "%s %s cells + @", $3.code, $4.code);
                                            $$.code = gen_code(temp);}
            | '(' IDENTIF ')'             { $$ = $2 ; }
            ;


%%                            // SECTION 4    Code in C

int n_line = 1 ;

void yyerror (char *message)
{
    fprintf (stderr, "%s in line %d\n", message, n_line) ;
    printf ( "\n") ;
}

char *int_to_string (int n)
{
    sprintf (temp, "%d", n) ;
    return gen_code (temp) ;
}

char *char_to_string (char c)
{
    sprintf (temp, "%c", c) ;
    return gen_code (temp) ;
}

char *my_malloc (int nbytes)     // reserve n bytes of dynamic memory 
{
    char *p ;
    static long int nb = 0 ;     // used to count the memory  
    static int nv = 0 ;          // required in total 

    p = malloc (nbytes) ;
    if (p == NULL) {
      fprintf (stderr, "No memory left for additional %d bytes\n", nbytes) ;
      fprintf (stderr, "%ld bytes reserved in %d calls \n", nb, nv) ;  
      exit (0) ;
    }
    nb += (long) nbytes ;
    nv++ ;

    return p ;
}



/***************************************************************************/
/***************************** Keyword Section *****************************/
/***************************************************************************/

typedef struct s_keyword { // for the reserved words of C  
    char *name ;
    int token ;
} t_keyword ;

t_keyword keywords [] = {     // define the keywords 
    "main",        MAIN,      // and their associated token  
    "int",         INTEGER,
    "setq",         SETQ,
    "print",        PRINT,
    "defun",        DEFUN,
    "prin1",        PRIN1,
    "do",           DO,
    "loop",         LOOP,
    "while",        WHILE,
    "if",           IF,
    "progn",        PROGN,
    "and",          AND,
    "or",           OR,
    "not",          NOT,
    "/=",           DIFF,
    "<=",           LTE,
    ">=",           GTE,
    "mod",          MOD,
    "setf",         SETF,
    "make-array",   MAKE_ARRAY,
    "aref",         AREF,
    "return-from",  RETURN_FROM,
    NULL,          0          // 0 to mark the end of the table
} ;

t_keyword *search_keyword (char *symbol_name)
{                       // Search symbol names in the keyword table
                        // and return a pointer to token register
    int i ;
    t_keyword *sim ;

    i = 0 ;
    sim = keywords ;
    while (sim [i].name != NULL) {
	    if (strcmp (sim [i].name, symbol_name) == 0) {
                                   // strcmp(a, b) returns == 0 if a==b  
            return &(sim [i]) ;
        }
        i++ ;
    }

    return NULL ;
}

 
/***************************************************************************/
/******************** Section for the Lexical Analyzer  ********************/
/***************************************************************************/

char *gen_code (char *name)   // copy the argument to an  
{                             // string in dynamic memory  
    char *p ;
    int l ;
	
    l = strlen (name)+1 ;
    p = (char *) my_malloc (l) ;
    strcpy (p, name) ;
	
    return p ;
}


int yylex ()
{
    int i ;
    unsigned char c ;
    unsigned char cc ;
    char expandable_ops [] =  "!<=>|/%&+-*" ;
    char temp_str [256] ;
    t_keyword *symbol ;

    do { 
        c = getchar () ; 
        if (c == '#') { // Ignore the lines starting with # (#define, #include) 
            do { // WARNING that it may malfunction if a line contains # 
                c = getchar () ; 
            } while (c != '\n') ; 
        } 
        if (c == '/') { // character / can be the beginning of a comment. 
            cc = getchar () ; 
            if (cc != '/') { // If the following char is / is a comment, but.... 
                ungetc (cc, stdin) ; 
            } else { 
                c = getchar () ; // ... 
                if (c == '@') { // Lines starting with //@ are transcribed
                    do { // This is inline code (embedded code in C).
                        c = getchar () ; 
                        putchar (c) ; 
                    } while (c != '\n') ; 
                } else { // ==> comment, ignore the line 
                    while (c != '\n') { 
                        c = getchar () ; 
                    } 
                } 
            } 
        } 
        if (c == '\n') 
            n_line++ ; 
    } while (c == ' ' || c == '\n' || c == 10 || c == 13 || c == '\t') ;

    if (c == '\"') {
        i = 0 ;
        do {
            c = getchar () ;
            temp_str [i++] = c ;
        } while (c != '\"' && i < 255) ;
        if (i == 256) {
            printf ("WARNING: string with more than 255 characters in line %d\n", n_line) ; 
        } // we should read until the next “, but, what if it is  missing? 
        temp_str [--i] = '\0' ;
        yylval.code = gen_code (temp_str) ;
        return (STRING) ;
    }

    if (c == '.' || (c >= '0' && c <= '9')) {
        ungetc (c, stdin) ;
        scanf ("%d", &yylval.value) ;
//         printf ("\nDEV: NUMBER %d\n", yylval.value) ;       
        return NUMBER ;
    }

    if ((c >= 'A' && c <= 'Z') || (c >= 'a' && c <= 'z')) {
        i = 0 ;
        while (((c >= 'A' && c <= 'Z') || (c >= 'a' && c <= 'z') ||
            (c >= '0' && c <= '9') || c == '_' || c == '-') && i < 255) {
        temp_str [i++] = tolower (c) ; // ALL TO SMALL LETTERS
        c = getchar () ; 
    } 
    temp_str [i] = '\0' ; // End of string  
    ungetc (c, stdin) ; // return excess char  

    yylval.code = gen_code (temp_str) ; 
    symbol = search_keyword (yylval.code) ;
    if (symbol == NULL) { // is not reserved word -> iderntifrier  
//               printf ("\nDEV: IDENTIF %s\n", yylval.code) ;    // PARA DEPURAR
            return (IDENTIF) ;
        } else {
//               printf ("\nDEV: OTRO %s\n", yylval.code) ;       // PARA DEPURAR
            return (symbol->token) ;
        }
    }

    if (strchr (expandable_ops, c) != NULL) { // // look for c in expandable_ops
        cc = getchar () ;
        sprintf (temp_str, "%c%c", (char) c, (char) cc) ;
        symbol = search_keyword (temp_str) ;
        if (symbol == NULL) {
            ungetc (cc, stdin) ;
            yylval.code = NULL ;
            return (c) ;
        } else {
            yylval.code = gen_code (temp_str) ; // although it is not used   
            return (symbol->token) ;
        }
    }

//    printf ("\nDEV: LITERAL %d #%c#\n", (int) c, c) ;      // PARA DEPURAR
    if (c == EOF || c == 255 || c == 26) {
//         printf ("tEOF ") ;                                // PARA DEPURAR
        return (0) ;
    }

    return c ;
}


int main ()
{
    yyparse () ;
}
